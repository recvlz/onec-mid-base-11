#Область ПрограммныйИнтерфейс

//ДополнитьФорму(Форма) добавляет поле в формы документов
//Параметры:
//Форма - Форма
Процедура ДополнитьФорму(Форма) Экспорт
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПолеСогласованнаяСкидкаИКнопкуПересчитатьТаблицу(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Нет_КонтактноеЛицо";
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Нет_КонтактноеЛицо";
КонецПроцедуры 

Процедура ДобавитьПолеСогласованнаяСкидкаИКнопкуПересчитатьТаблицу(Форма)
	Группа = Форма.Элементы.Добавить("ГруппаСкидки", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.ОтображатьЗаголовок = Ложь;
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка",Тип("ПолеФормы"),Форма.Элементы.ГруппаСкидки);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Нет_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "ПересчитатьТаблицу";
	
	Кнопка = Форма.Элементы.Добавить("ПересчитатьТаблицу", Тип("КнопкаФормы"),Форма.Элементы.ГруппаСкидки);
	Кнопка.ИмяКоманды = "ПересчитатьТаблицу";
	Кнопка.Картинка = БиблиотекаКартинок.Обновить;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
КонецПроцедуры

#КонецОбласти